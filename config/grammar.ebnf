(* 
The Formal Whirlwind Language Grammar
-------------------------------------
- This grammar is complex and is technically not LALR(1) in certain locations
  but some adjustments to the parsing algorithm and parser generator allow for
  these conflicts to either be ignored or automatically resolved.
- Indentation is managed by the lexer which produces the tokens INDENT and
  DEDENT to denote changes in the level of indentation (an increase and a
  decrease respectively)
- NEWLINE is as the name would suggest a newline.  However, it does not
  incorporate a carriage return, and it is produced separately from indentation
  changes (ie. DEDENT happens after a NEWLINE).
- All other tokens in all caps are simply special tokens produces by the lexer.
  All tokens in lowercase are just raw strings.  However, all keyword tokens
  assert an implicit word-boundary.   
- The `? ... ?` syntax marks a group that can be optionally wrapped in a block
  (indentation suite - eg. arrays, type definitions, lists, dictionaries).
- No need to specify NEWLINE before every INDENT because a NEWLINE must be there
  in order for an INDENT to be registered and the parser simply ignores unexpected
  newlines (actually less work for everyone).
*)

(* file is our goal symbol *)
file = {annotation} {export_stmt} {import_stmt} (export_block { export_block } [ top_level ] | top_level) ;

(* export_stmt designates a remote symbol export -- built on top of import statements *)
export_stmt = 'export' ( identifier_list | '...' ) 'from' package_name 'NEWLINE' ;

import_stmt = 'import' ( package_name ['as' 'IDENTIFIER'] | ( identifier_list | '...' ) 'from' package_name ) 'NEWLINE' ;
package_name = 'IDENTIFIER' { '::' 'IDENTIFIER' } | '.' ;

export_block = 'export' 'of' 'INDENT' top_level 'DEDENT' ;

top_level = definition { definition } ;
definition = interf_def | interf_bind | operator_def | func_def | type_def 
  | decorator | annotated_def | variable_decl | variant_def ;

interf_def = 'interf' 'IDENTIFIER' [generic_tag] 'of' interf_body ;
interf_bind = 'interf' [generic_tag] 'for' type [ 'is' type {',' type}] interf_body ;
interf_body = 'INDENT' interf_member { interf_member } 'DEDENT' ;
interf_member = func_def | variant_def | method_variant | annotated_method ;
method_variant = 'variant' [generic_tag] 'when' method_variant_cond {',' method_variant_cond} 'NEWLINE' func_def ;
method_variant_cond = 'IDENTIFIER' 'is' type ;

operator_def = 'operator' '(' operator_value ')' [generic_tag] signature func_body ;
operator_value = '+' | '-' | '*' | '/' | '~/' | '~*' 
  | '~^' | '%' | '>' | '>>' | '<' | '<<' | '<=' | '>=' 
  | '==' | '!' | '!=' | '&' | '&&' | '|' | '||' | '^' 
  | '~' | '[' [':'] ']' ;

func_def = ('func' | 'async') 'IDENTIFIER' [generic_tag] [signature] [func_body] ;
signature = [args_decl] type ;
args_decl = '(' (arg_decl {',' arg_decl } [',' var_arg_decl] | var_arg_decl ) ')' ;
arg_decl = ['const'] ['vol'] identifier_list type_ext [initializer] ;
var_arg_decl = '...' 'IDENTIFIER' type_ext ;
func_body = ['const'] ( do_block | ? '=>' expr ? ) ;

type_def = ['closed'] 'type' 'IDENTIFIER' [generic_tag] ( ? typeset ? | newtype ) ;
typeset = '=' type {'|' type } ;
newtype = ? alg_suffix ? | struct_suffix ;
alg_suffix = alg_type_member {alg_type_member} ;
alg_type_member = '|' 'IDENTIFIER' [tupled_suffix] ;
tupled_suffix = '(' type {',' type} ')' ;
struct_suffix = ['of' type] '{' struct_member { struct_member } '}' ;
struct_member = ['const'] ['vol'] identifier_list type_ext [initializer] 'NEWLINE' ;

(* expr can be a list of decorators *)
decorator = '@' expr 'NEWLINE' func_def ;

annotated_def = annotation { annotation } (type_def | func_def | interf_def | interf_bind | operator_def) ;
annotated_method = annotation { annotation } func_def ;
annotation = '#' 'IDENTIFIER' [ 'STRINGLIT' ] 'NEWLINE' ;

variant_def = 'variant' '<' type_list '>' 'IDENTIFIER' func_body ;

(* ellipsis is used to designate a block that hasn't been filled in yet *)
do_block = 'do' block ;
block = 'INDENT' ( block_content {block_content} | '...') 'DEDENT' ;
block_content = simple_stmt 'NEWLINE' | complex_stmt | func_def ;

(* complex statements contain a block (eg. an if statement) *)
complex_stmt = if_chain | match_stmt | for_loop | while_loop 
  | ctx_manager | async_for_loop | region_def ;

if_chain = 'if' [variable_decl ';'] expr do_block {elif_block} [else_block] ;
elif_block = 'elif' [variable_decl ';'] expr do_block ;
else_block = 'else' (do_block | block) ;

match_stmt = 'match' expr (type_match_block | val_match_block) ;
type_match_block = 'type' 'to' 'INDENT' type_case_block {type_case_block} 'DEDENT' ;
val_match_block = 'to' 'INDENT' val_case_block {val_case_block} 'DEDENT' ;
type_case_block = 'case' type_pattern_list [when_cond] do_block ;
val_case_block = 'case' expr_list [when_cond] do_block ;

for_loop = 'for' iterator do_block [nobreak_clause] ;
async_for_loop = 'async' 'for' iterator do_block [nobreak_clause] ;
while_loop = 'while' [variable_decl ';'] expr do_block [nobreak_clause] ;
nobreak_clause = 'nobreak' (do_block | block) ;

ctx_manager = 'with' ? full_ctx ? do_block [else_block] [ctx_else_clause] ;
full_ctx = ctx_elem {semilinebr ctx_elem} ;
ctx_elem = identifier_list '<-' expr ;
ctx_else_clause = 'else' (block | do_block | 'match' val_match_block) ;

region_def = 'region' 'of' block ;

(* simple statements do not contain a block - newline suffix is added in block *)
simple_stmt = 
  break_stmt | continue_stmt | fallthrough_stmt | return_stmt 
  | yield_stmt | expr_stmt | variable_decl ;

break_stmt = 'break' ;
continue_stmt = 'continue';
fallthrough_stmt = 'fallthrough' ['to' 'match'] ;
return_stmt = 'return' [ expr_list ] ;
yield_stmt = 'yield' expr_list ;

variable_decl = ('let' | 'const') ['vol'] ? (unpack_var (type_ext | initializer) | var {',' var} ) ? ;
var = 'IDENTIFIER' [type_ext] [initializer];
unpack_var = '(' unpack_elem {',' unpack_elem} [',' '...'] ')' ;
unpack_elem = 'IDENTIFIER' | unpack_var ;

expr_stmt = mut_expr {',' mut_expr} [assign_op expr_list | '++' | '--'] ;
mut_expr = ['await'] ['*'] mut_atom {trailer} ;
mut_atom = 'IDENTIFIER' | '(' expr ')' ;
assign_op = ['+' | '-' | '*' | '/' | '%' | '~/' | '~^' | '>>' | '<<' | '&' | '|' | '^'] '=' ;

expr = (match_expr ['if' match_expr 'else' expr ] | closure | with_expr) ;
closure = ['async'] ('||' | '|' [closure_arg {',' closure_arg}] '|' func_body) ;
closure_arg = ['const'] 'IDENTIFIER' [type_ext] ;

match_expr = core_expr ['match' core_expr ] | 'match' core_expr inline_match_suffix ;
inline_match_suffix = 'to' inline_match_expr_block | 'type' 'to' inline_match_type_block ;
inline_match_expr_block = 'INDENT' inline_match_expr_branch {inline_match_expr_branch} 'DEDENT' ;
inline_match_expr_branch = expr_list '=>' expr 'NEWLINE' ;
inline_match_type_block = 'INDENT' inline_match_type_branch {inline_match_type_branch} 'DEDENT' ;
inline_match_type_branch = type_pattern_list '=>' expr 'NEWLINE' ;

with_expr = 'with' ? full_ctx ? '=>' expr ;

core_expr = ['await'] func_op [core_expr_suffix] ;
core_expr_suffix = '..' func_op
  | 'as' type {'as' type}
  | 'is' (type_pattern | '!' type) ;

func_op = or_expr {'~*' or_expr} ;
or_expr = xor_expr {('||' | '|') xor_expr} ;
xor_expr = and_expr {'^' and_expr} ;
and_expr = comp_expr {('&&' | '&') and_expr} ;
comp_expr = shift_expr {comp_op shift_expr} ;
comp_op = '<' | '>' | '>=' | '<=' | '==' | '!=' ;
shift_expr = arith_expr {('>>' | '<<') arith_expr} ;
arith_expr = term {('+' | '-') term} ;
term = factor {('*' | '/' | '~/' | '%') factor} ;
factor = [unary_op] power ;
unary_op = '&' ['const'] | '*' | '-' | '~' | '$' ;
power = atom_expr {'~^' atom_expr} ;

atom_expr = atom {trailer} | make_expr ;
make_expr = 'make' region_spec alloc_param ;
region_spec = 'local' | 'nonlocal' [nonlocal_spec] | 'in' '[' 'IDENTIFIER' '] | 'global' ;
nonlocal_spec = '[' ('region' | 'func') ']';
alloc_param = type | '(' type ',' expr ')' | 'IDENTIFIER' '{' [init_list] '}';

trailer = '.' ( 'IDENTIFIER' | 'INTLIT' )
  | '?'
  | '[' expr [slice_suffix] ']'
  | '{' [init_list] '}' 
  | '(' [args_list] ')'
  | '::' 'IDENTIFIER' ;
slice_suffix = ':' expr [':' expr] ;
init_list = '...' expr {',' init_member_expr} | init_member_expr {',' init_member_expr} ;
init_member_expr = 'IDENTIFIER' '=' expr;
args_list = arg {',' arg} ;
arg = expr ['=' expr];

atom = 'INTLIT' | 'STRINGLIT' | 'FLOATLIT' | 'BOOLLIT' | 'RUNELIT' | array_builder 
  | list_builder | dict_builder | vector_builder | tupled_expr | 'IDENTIFIER' | 'null' ;

array_builder = '{' seq_contents '}' ;
list_builder = '[' seq_contents ']' ;
seq_contents = expr ({',' expr} | [comp_suffix]) ;
dict_builder = '{' expr ':' expr ({',' expr ':' expr} | [comp_suffix]) '}' ;
vector_builder = '<' expr [':' expr] '>' ; (* expr can be an array for vector array initializers *)
tupled_expr = '(' expr {',' expr} ')' ;

(* type constructions *)
type_list = type (',' type ) ;
type_ext = ':' type ;
type_pattern_list = type_pattern {',' type_pattern};
type_pattern = ['IDENTIFIER' ':'] type ;

type = value_type | named_type | ref_type | region_type ;
named_type = 'IDENTIFIER' {'::' 'IDENTIFIER'} ['<' type_list '>'];

value_type = prim_type | col_type | vec_type | func_type | tup_type ;
prim_type = 'int' | 'uint' | 'ushort' | 'short' | 'ulong' | 'long' | 'nothing'
  | 'sbyte' | 'sbyte' | 'string' | 'rune' | 'float' | 'double' | 'bool' | 'any' ;
col_type = '[' type [':' type] ']' | '[' ']' type ;
vec_type = '<' 'INTLIT' '>' type ;
func_type = ('func' | 'async') '(' [func_type_args] ')' '(' [type_list] ')' ;
func_type_args = (func_type_arg {',' func_type_arg} [',' func_type_var_arg] | func_type_var_arg) ;
func_type_arg = ['~'] type ;
func_type_var_arg = '...' type ;
tup_type = '(' type ',' type {',' type} ')' ;

ref_type = dynamic_ref | base_ref ;
dynamic_ref = ['global'] ('own' base_ref | block_ref) ;
base_ref = '&' ['const'] (value_type | named_type) ;
block_ref = '[' '&' ']' type ;

region_type = 'region' ;

(* generic constructions *)
generic_tag = '<' generic_param {',' generic_param} '>' ;
generic_param = 'IDENTIFIER' [':' type {'|' type}];

(* iterable constructions *)
comp_suffix = 'for' iterator [when_cond] ;
iterator = iter_var {',' iter_var} 'in' expr ;
iter_var = 'IDENTIFIER' | unpack_var ;

(* other bits and bobs *)
expr_list = expr {',' expr} ;
when_cond = 'when' expr ;
initializer = '=' expr ;
semilinebr = ';' | 'NEWLINE' ;
identifier_list = 'IDENTIFIER' {',' 'IDENTIFIER'} ;
