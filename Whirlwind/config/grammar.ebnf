// WHIRLWIND STANDARD GRAMMAR
whirlwind: ( variable_decl ';'
| block_decl
| include_stmt ';'
| export_decl
)+ ;

main: ( func_decl
| block_stmt 
| stmt
| subscope
)+ ;

subscope: '{' main '}' ;

// include statement
include_stmt: 'INCLUDE' ( pkg_name ['STRING_LITERAL'] | include_set 'FROM' pkg_name ) ;
pkg_name: [pkg_back_move] 'IDENTIFIER' (':' ':' 'IDENTIFIER' | pkg_back_move 'IDENTIFIER')* ;
pkg_back_move: '.' '.' ;
include_set: '{' 'IDENTIFIER' (',' 'IDENTIFIER')* '}' ;

// export declaration
export_decl: 'EXPORT' ( block_decl
| include_stmt ';'
| variable_decl ';'
);

// BLOCK DECLARATIONS
block_decl: func_decl
| interface_decl
| type_class_decl
| struct_decl
| template_decl
| variant_decl
| decor_decl
;

// function declaration
func_decl: ('FUNC' | 'ASYNC') 'IDENTIFIER' '(' args_decl_list ')' [ types ] (func_body | ';') ;

// interface declaration
interface_decl: 'INTERF' interface_header '{' interface_main '}' ;
interface_header: 'IDENTIFIER' | 'FOR' types ['IS' implements] ;
implements: types (',' types)* ;
interface_main: ( func_decl | variant_decl | method_template | operator_decl )+ ;

// operator overload
operator_decl: 'OPERATOR' ( operator | ext_op ) '(' args_decl_list ')' [types] (func_body | ';') ;
ext_op: '[' [ ':' ] ']' | '<- ;

// method template
method_template: 'TEMPLATE' '<' template (',' template)* '>' func_decl ;

// type class declaration
type_class_decl: 'TYPE' 'IDENTIFIER' [ type_class_main | ( '|' type_class_main )+ ] ';' ;
type_class_main: types [value_restrictor] | 'IDENTIFIER' [type_constructor] ;
value_restrictor: '{' 'IDENTIFIER' '}' 'WHEN' expr ;
type_constructor: '(' type_list ')' ;

// struct declaration
struct_decl: 'STRUCT' 'IDENTIFIER' '{' struct_main '}' ;
struct_main: (struct_var ';' | constructor_decl )+ ;
struct_var: ['VOL'] 'IDENTIFIER' (',' 'IDENTIFIER')* ':' types [initializer];

// constructor declaration
constructor_decl: 'CONSTRUCTOR' '(' args_decl_list ')' func_body ;

// template declaration
template_decl: 'TEMPLATE' '<' template (',' template)* '>' template_block_decl ;
template: 'IDENTIFIER' [':' type_list] ;
template_block_decl: func_decl
| type_class_decl
| struct_decl
| interface_decl
| decor_decl
;

// variant declaration
variant_decl: 'VARIANT' ( variant | variant_list ) 'IDENTIFIER' func_body ;
variant_list: '(' variant (',' variant)+ ')' ;
variant: '<' type_list '>' ;

// decorator declaration
decor_decl: decor func_decl ;
decor: '@' '[' expr (',' expr)* ']' ;


// BLOCK STATEMENTS (control flow and context managers)
block_stmt: if_stmt
| select_stmt
| for_loop
| from_block
;

// if statement
if_stmt: 'IF' '(' expr ')' block elif_block* [else_block] ;
elif_block: 'ELIF' '(' expr ')' block ;
else_block: 'ELSE' block ;

// select statement
select_stmt: 'SELECT' '(' expr ')' '{' select_block '}' ;
select_block: case+ [default] ;
case: 'CASE' expr (',' expr)* ':' main ;
default: 'DEFAULT' ':' main ;

// for loop
for_loop: 'FOR' [for_body] block [after_clause] ;
for_body: '(' (c_for | iterator | expr) ')' ;
c_for: ['IDENTIFIER' '=' expr] ';' [expr] ';' [expr | assignment] ;

// from block
from_block: 'FROM' from_body [after_clause] ;
from_body: '(' variable_decl ')' block ;

// after clause
after_clause: 'AFTER' block ;


// STATEMENTS
// broken up for logical reasons - takes advantage of multiple solution parsing
stmt: ( variable_decl
| assignment
| expr
| throw_stmt 
| continue_stmt
| break_stmt
| delete_stmt
| return_stmt
| yield_stmt
) ';' 
;

// variable declaration
variable_decl: ('LET' | 'CONST') ['VOL'] var extension variable_initializer  ;
var: '(' var_id (',' var_id)* ')'
| 'IDENTIFIER'
;
var_id: 'IDENTIFIER' extension variable_initializer
| '_'
;

variable_initializer: [ '=' expr
| ':=' expr
] ;

// variable assignment
assignment: assign_var (',' assign_var)* assign_op expr (',' expr)* ;
assign_var: '*'* ( 'IDENTIFIER' | 'THIS' | '_' ) trailer* ;

assign_op: [ operator ] '=' ;

// simple statements
continue_stmt: 'CONTINUE' ;
break_stmt: 'BREAK' ;
delete_stmt: 'DELETE' 'IDENTIFIER' (',' 'IDENTIFIER')* ;
return_stmt: 'RETURN' [expr (',' expr)*] ;
yield_stmt: 'YIELD' expr (',' expr)* ;


// EXPRESSIONS - basic unit of statements 
expr: func_op [expr_extension] | expr_var ;
expr_extension: 'IF' expr 'ELSE' expr 
| case_extension+
| 'IS' types
| ':>' expr
| then_extension
; 

// then extension
then_extension: 'THEN' expr ;

// case extension
case_extension: 'CASE' '{' inline_case+ default_case '}' ;
inline_case: expr (',' expr)* '=>' expr ',' ;
default_case: '_' '=>' expr ;

// expression local variable declaration
expr_var: 'IDENTIFIER' '=' expr then_extension ;

// functional/monadic operators
func_op: or (('>>=' | '~*') or)* ;

// logical operators
or: xor ('OR' xor)* ;
xor: and ('XOR' and)* ;
and: comparison ('AND' comparison)* ;

// comparisons
comparison: not (comparison_op not)*;
comparison_op: '>' | '<' | '>=' | '<=' | '==' | '!=' ;
not: ['!'] shift ;

// binary shifts
shift: arithmetic (shift_op arithmetic)* ;
shift_op: '>' '>' | '<' '<';

// arithmetic
arithmetic: term (('+' | '-') term)* ;
term: factor (('*' | '/' | '%' | '~/') factor)* ;
factor: unary_atom ('~^' unary_atom)*;

// unary operators
unary_atom: ('REF' | '&' | '-' | '~' | '++' | '--' | '*'+) atom
| atom ['++' | '--']
;


// ATOMS - basic unit of expressions
atom: [ 'NEW' ] [ 'AWAIT' ] base trailer*
| comprehension trailer*
| heap_alloc
| from_expr
;

trailer: '[' (expr [slice] | slice) ']'
| '(' args_list ')'
| '{' init_list '}'
| '.' ( 'IDENTIFIER' | 'INTEGER_LITERAL' )
| '->' 'IDENTIFIER'
| template_spec
| static_get
;

// trailer components
slice: ':' [expr [':' expr] | ':' expr] ;

args_list: [arg (',' arg)*] ;
arg: named_arg | expr ;
named_arg: 'IDENTIFIER' '=' expr ;

init_list: 'IDENTIFIER' initializer (',' 'IDENTIFIER' initializer)* ;
static_get: get_op 'IDENTIFIER' ;
get_op: ':' ':' ;

// comprehension
comprehension: '{' comp_body '}' | '[' comp_body ']' ;
comp_body: expr [':' expr] '|' iterator ['WHEN' expr] ;

// allocate a type on the heap
heap_alloc: 'MAKE' alloc_body ;
alloc_body: '(' types ',' expr ')'
| types 
| base trailer
;

// access the value of a type class
from_expr: 'FROM' expr ;

// BASES - lowest component of an atom
base: 'INTEGER_LITERAL'
| 'FLOAT_LITERAL'
| 'BOOL_LITERAL'
| 'STRING_LITERAL'
| 'CHAR_LITERAL'
| 'HEX_LITERAL'
| 'BINARY_LITERAL'
| 'THIS'
| 'IDENTIFIER'
| 'NULL'
| 'VAL'
| array
| list
| dict
| tuple
| closure
| sub_expr
| type_cast
| partial_func
| range
;

// collection types
array: '{' expr ( ',' expr )* '}' ;
list: '[' expr ( ',' expr )* ']' ;
dict: '{' expr ':' expr (',' expr ':' expr)* '}' ;

// tuple literal
tuple: '(' expr (',' expr)+ ')' ;

// inline functions (closures)
closure: '|' args_decl_list '|' closure_body ;
closure_body: '=>' expr
| '{' [main] '}'
;

// sub expr declared so that it could be easier to parse
sub_expr: '(' expr ')' ;

// casting syntax includes call
type_cast: 'CAST' '<' types '>' '(' expr ')' ;

// convert function to partial function
partial_func: '|' expr partial_arg (',' partial_arg)* ')' ;
partial_arg: '_'
| expr
;

// range literal
range: expr '.' '.' expr ;


// DATA TYPES - data type literals
types: ['OWN'] ['REF'] '*'* ( base_types
| 'IDENTIFIER' [template_spec]
)
;

base_types: simple_types
| collection_types
| func_types
| tuple_type
;

simple_types: 'BOOL_TYPE'
| 'FLOAT_TYPE'
| 'INT_TYPE'
| 'STRING_TYPE'
| 'CHAR_TYPE'
| 'BYTE_TYPE'
| 'LONG_TYPE'
| 'DOUBLE_TYPE'
| 'VOID_TYPE'
;

collection_types: '[' [ expr ] ']' types
| '[' types ']'
| '[' types ':' types ']'
;

func_types: ('FUNC' | 'ASYNC') '(' func_arg_types ')' '(' [ type_list ] ')' ;
func_arg_types: [ func_arg_type (',' func_arg_type)* ] [func_arg_indef] ;
func_arg_type: types ['~'] ;
func_arg_indef: '...' [types] ;

tuple_type: '(' tuple_type_list ')' ;
tuple_type_list: types (',' types)+ ;


// COMMON CONSTRUCTIONS - common structures used repeatedly in grammar
// a series of types delimited by commas
type_list: types (',' types)* ;

// argument declaration syntax
args_decl_list: [ decl_arg (',' decl_arg)* [',' ending_arg] | ending_arg ] ;
decl_arg: ['VOL'] ['CONST'] 'IDENTIFIER' (',' 'IDENTIFIER')* extension [initializer] ;
ending_arg: '...' 'IDENTIFIER' extension ;

// general purpose initializer
initializer: '=' expr ;

// iterator syntax
iterator: iter_var (',' iter_var)* '<-' expr ;
iter_var: 'IDENTIFIER' | '_' ;

// template specifier
template_spec: '<' type_list '>' ;

// type extension
extension: [':' types] ;

// function body
func_body: [func_guard] ('{' [main] '}'
| '=>' expr (',' expr)* ';'
)
;

// function guard
func_guard: 'WHEN' '(' expr ')' ;

// standard block
block: '{' [main] '}'
| stmt
;

// operator (wherever any operator can be used)
operator: '+' | '-' | '*' | '/' | '%' | '<' | '>' | '<=' | '>=' | '==' | '!='
| '>>=' | '~^' | '~*' | '~/' | '<' '<' | '>' '>' | 'AND' | 'OR' | 'XOR' | '~' ;
